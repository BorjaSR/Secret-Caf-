apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    defaultConfig {
        applicationId "es.bsalazar.secretcafe"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {

        debug {
            debuggable true
            buildConfigField "String", "FIREBASE_PREFIX", "\"DEV_\""
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "FIREBASE_PREFIX", "\"\""
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "role"
    productFlavors {
        admin {
            resValue("string", "app_name", "Secret Café Admin")
            buildConfigField "boolean", "Admin", "true"
        }

        user {
            resValue("string", "app_name", "Secret Café")
            buildConfigField "boolean", "Admin", "false"
        }
    }

    applicationVariants.all { variant ->
        if (variant.getName() == "userRelease") {
            variant.outputs.all { output ->
                outputFileName = "SecretCafé-User-${variant.versionName}_${variant.versionCode}.apk"
            }
        } else if (variant.getName() == "adminRelease") {
            variant.outputs.all { output ->
                outputFileName = "SecretCafé-Admin-${variant.versionName}_${variant.versionCode}.apk"
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //noinspection GradleCompatible
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.google.android.gms:play-services-location:12.0.1'
    implementation "com.google.android.gms:play-services-gcm:12.0.1"
    //FIREBASE
    implementation "com.google.firebase:firebase-firestore:$firebaseVersion"
    implementation "com.google.firebase:firebase-storage:$firebaseVersion"
    implementation "com.google.firebase:firebase-auth:$firebaseVersion"
    // FirebaseUI Storage only
    implementation 'com.firebaseui:firebase-ui-storage:0.6.0'
    //Design
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    //View bindings
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    //COLOR PICKER
    implementation 'com.github.QuadFlask:colorpicker:0.0.13'
    //Circle ImageView
    implementation 'de.hdodenhof:circleimageview:2.1.0'
    //Architecture components
    implementation "android.arch.lifecycle:runtime:$archComponentsVersion"
    implementation "android.arch.lifecycle:extensions:$archComponentsVersion"
    annotationProcessor "android.arch.lifecycle:compiler:$archComponentsVersion"
    //ZXING
    implementation 'me.dm7.barcodescanner:zxing:1.9.3'
}




apply plugin: 'com.google.gms.google-services'